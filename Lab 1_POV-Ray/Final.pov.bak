#version  3.7;
global_settings { assumed_gamma 2.2 }

#include "shapes.inc"
#include "colors.inc"
#include "textures.inc"

union {
// Wooden torus
torus { 7.0, 3.0
   // Bottom layer texture.  Uses a "stretched" bozo for fine porous grain
   texture {
      pigment {
         bozo
         color_map {
            [0.0 0.4 color BakersChoc  color BakersChoc ]
            [0.4 1.01 color Tan color Tan]
         }
         scale <4, 0.05, 0.05>
      }
   }

   // Overlaying ring grain texture
   texture {
      finish {
         phong 1
         phong_size 100
         brilliance 3
         ambient 0.2
         diffuse 0.8
      }
      pigment {
         wood
         turbulence 0.025

         color_map {
            [0.0 0.15 color SemiSweetChoc color CoolCopper ]
            [0.15 0.40 color CoolCopper color Clear ]
            [0.40 0.80 color Clear  color CoolCopper ]
            [0.80 1.01 color CoolCopper color SemiSweetChoc ]
         }

         scale <3.5, 1, 1>
         translate -50*y
         rotate 1.5*z
      }
   }
}

difference {
  lathe {
    linear_spline

    12,

    <2, 1>,
    <2, -1>, <3, -1>, <3.4, -2>, <4, -1.1>, <3.6, -0.9>,
    <2.6, 0>,
    <3.6, 0.9>, <4, 1.1>, <3.4, 2>, <3, 1>, <2, 1>

    pigment {
      color Red
    }
    finish {
      ambient 0.1
      diffuse 0.6
      phong 0.6
      phong_size 7
//      reflection 0.3
    }
  }
  box {
    <0, -5, 0>, <5, 5, -5>
    rotate <0, 30, 0>
    pigment {
      color rgb<1,1,0.7>*1.2 
    }
    finish {
      ambient 0.1
      diffuse 0.6
      phong 0.6
      phong_size 7
    }
  }
}
}


// Main light source
light_source { <-50.0, 100, -80.0> colour White }

// Dim side light to fill shadows
light_source { <250.0, 25.0, -100.0> colour DimGray }


camera {
   location <0.0, 20.0, -30.0>
   angle 55 
   right     x*image_width/image_height
   look_at <0, 0, 0>
}

plane { y , -2 
    pigment { 
    checker 
    colour rgb < 10 , 10 , 10 > colour rgb < 0.5 , 0.5 , 0.5 > 
    }
} 

